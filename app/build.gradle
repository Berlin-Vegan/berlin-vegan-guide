apply plugin: 'com.android.application'
apply plugin: 'android-apt'


android {
    def gitTag = 'git describe --exact HEAD'.execute().text.trim()
    def gitHash = 'git rev-parse --short HEAD'.execute().text.trim()
    def gitBranch = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
    def hasModifiedDeletedOrOtherFiles = !'git ls-files -mdo --exclude-standard'.execute().text.trim().isEmpty()
    def hasStagedFiles = !'git diff-index --no-ext-diff --name-only --cached HEAD'.execute().text.trim().isEmpty()
    def dirtyWorkingCopy = hasModifiedDeletedOrOtherFiles || hasStagedFiles
    def gitDescription = dirtyWorkingCopy ? "${gitHash}-dirty" : gitHash

    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    android {
        useLibrary 'org.apache.http.legacy'
    }
    defaultConfig {
        applicationId "org.berlin_vegan.bvapp"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 27
        versionName gitTag ?: gitBranch
        if (versionName) {
            buildConfigField "String", "VERSION_GIT_DESCRIPTION", "\"${versionName}-${gitDescription}\""
        } else {
            buildConfigField "String", "VERSION_GIT_DESCRIPTION", "\"empty version name\""
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }
    }
}
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:design:24.1.1'
    compile 'com.android.support:appcompat-v7:24.1.1'
    compile 'com.android.support:recyclerview-v7:24.1.1'

    compile 'com.google.code.gson:gson:2.7'
    compile('com.afollestad.material-dialogs:core:0.8.6.0@aar') {
        transitive = true
    }
    compile 'ch.acra:acra:4.9.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton:butterknife:8.2.1'
    apt 'com.jakewharton:butterknife-compiler:8.2.1'
    compile 'org.osmdroid:osmdroid-android:5.2@aar'

    testCompile 'junit:junit:4.12'
}
